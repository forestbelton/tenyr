#ifndef LIMIT
#define LIMIT 100
#endif
#
#define jeq(Cond,Label) p <- [p + Cond + 2] ; .word @Label ; .word . + 1 ;
#define jmp(Label) p <- p + @Label - .

    b <- LIMIT          // b = upper limit (N)
    d <- 1              // d = one
    c <- b >> d         // c = (N/2)
    i <- 2              // i = outer loop control
    o <- 0x1000         // base of array
    j <- 2              // j = inner index, setup index

    a -> [o + 0]        // 0 is not prime
    a -> [o + 1]        // 1 is not prime
init:
    d -> [o + j]        // write truth to table cell
    j <- j + 1          // increment table index
    f <- j <= b         // exit loop when i > N
    jeq(f,init)
outer:
    f <- i > c          // exit loop when i > N/2
    jeq(f,done)
    g <- [o + i]        // load compositeness
    f <- g == a         // check recorded compositeness
    jeq(f,bottom)
    k <- 2              // set up initial multiplier
inner:
    j <- i * k          // compute j
    k <- k + 1          // increment multiplier
    f <- j > b          // compare j to N
    jeq(f,bottom)
    a -> [o + j]        // mark as composite
    jmp(inner)

bottom:
    i <- i + 1          // check next candidate
    jmp(outer)

done:
    illegal

